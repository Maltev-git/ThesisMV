set terminal pdfcairo enhanced font "arial,12" fontscale 0.5 lw 1 size 8.8cm,6.6cm rounded
set output "Kp_2dhistogram_V_sws123_fit_f2.pdf"

datafile_sws1 = "../Data/OMNI_data3_analysis/KpvsV_sws1/OMNI_HRO_1MIN_19810101--20161231_with_ap_VBzgsm_sws1_3hmax_KpvsV_2Dhistogram.txt"
datafile2_sws1 = "../Data/OMNI_data3_analysis/KpvsV_sws1/OMNI_HRO_1MIN_19810101--20161231_with_ap_VBzgsm_sws1_3hmax_KpvsV_dependenceFunctionsYofX.txt"
datafile_sws23 = "../Data/OMNI_data3_analysis/KpvsV_sws23/OMNI_HRO_1MIN_19810101--20161231_with_ap_VBzgsm_sws23_3hmax_KpvsV_2Dhistogram.txt"
datafile2_sws23 = "../Data/OMNI_data3_analysis/KpvsV_sws23/OMNI_HRO_1MIN_19810101--20161231_with_ap_VBzgsm_sws23_3hmax_KpvsV_dependenceFunctionsYofX.txt"
datafile_sws23_shift = "../Data/OMNI_data3_analysis/KpvsV_sws23/OMNI_HRO_1MIN_19810101--20161231_with_ap_VBzgsm_sws23_3hmax_ap9hshift_KpvsV_2Dhistogram.txt"
datafile2_sws23_shift = "../Data/OMNI_data3_analysis/KpvsV_sws23/OMNI_HRO_1MIN_19810101--20161231_with_ap_VBzgsm_sws23_3hmax_ap9hshift_KpvsV_dependenceFunctionsYofX.txt"

#######- header parameters from datafile -#######
# Input data file: OMNI_HRO_1MIN_19810101--20161231_with_ap_VBzgsm_sws1_3hmax.txt
binSizeX=10
binSizeY=1
histLowX=255
histLowY=0
histUppX=1085
histUppY='28.0'
hist2DbinCount=183
intersectionCount=12115
peakCount=62
peakX=435
peakY='6.0'
meanCount=10
meanX=455
meanY='8'
###########


### log fits
f_CME(x) = 10.6 * log(x + 810.0) - 73.0
f_stream(x) = 5.88 * log(x + 299.0) - 37.0

v_CME(kp) = exp((kp + 73.0) / 10.6) - 810.0

####--  Styles  --####
set style line 1 lt 1 lw 1 lc rgb "white"
set style line 2 lt 1 lw 1 lc rgb "light-red"
set style line 3 lt 1 lw 1 lc rgb "web-green"
set style line 4 lt 1 lw 1 lc rgb "web-blue"
set style line 5 lt 1 lw 1 lc rgb "yellow"
set style line 6 lt 1 lw 1 lc rgb "brown"
set style line 7 lt 1 lw 1 lc rgb "light-blue"
set style line 8 lt 1 lw 1 lc rgb "dark-blue"
set style line 9 lt 1 lw 1 lc rgb "dark-violet"
set style line 10 lt 1 lw 1 lc rgb "black"
set style line 11 lt 1 lw 1 lc rgb "grey"
set style line 12 lt 1 lw 1 lc rgb "grey50"

set style line 40 lt 1 lw 1 lc rgb "#7f0080ff"
set style line 70 lt 1 lw 1 lc rgb "#7fadd8e6"
set style line 110 lt 1 lw 1 lc rgb "#7fc0c0c0"
set style line 120 lt 1 lw 1 lc rgb "#7f7f7f7f"

set style line 111 lt 1 lw 1 lc rgb "gray90"

set style textbox opaque noborder

set multiplot\
	layout 1,1 columnsfirst\
	margins 0.105,0.99,0.135,0.98\
	spacing 0,0

set border 3
set xtics nomirror
set ytics nomirror

set grid front
set nobars

set key reverse Left box opaque samplen 2 font ",11"
set key top left
set key height +0.1
set key width +0.8
#set key at screen 0.99,screen 0.68


#set log cb
set palette defined (0 "white", 3e4 "white", 3e4 "grey80", 1e6 "grey80")
unset colorbox

set view map

set arrow nohead from 0,9 to 2200,9 ls 10 lw 1 dt 2 front

set ylabel "{/:Italic= Kp} index" offset 2.0,0
set yrange [0:12]
set ytics 1
set mytics 3
set xrange [0:2200]
set xtics 400
set xtics add("" 2200)
set ytics add("(10)" 10,"(11)" 11,"(12)" 12)
set mxtics 4
set xlabel "Velocity [km s^{-1}]" offset 0,0.37

sigma1 = 1.113
sigma2 = 0.742
#set label at graph 0.1,graph 0.3 sprintf("sigma1 = %g",sigma1) font ",10" boxed front
k = 20.0
kk = 7.0
plot\
	for [i=-k:k] (f_CME(x)+i/k*sigma1)>=9?NaN:(f_CME(x)+i/k*sigma1) notitle with lines ls 11 lw 2 dt 1,\
	for [i=-kk:kk] (f_CME(x)+i/kk*sigma1)<=9?NaN:(f_CME(x)+i/kk*sigma1) notitle with lines ls 11 lw 2 dt 1,\
	for [i=-k:k] [0:1100] f_stream(x)+i/k*sigma2 notitle with lines ls 40 lw 1 dt 1,\
    f_CME(x) title "CME fit" with lines ls 10 lw 2 dt 2,\
	NaN title "CME mean MAD" with points ls 11 pt 5 ps 0.7,\
    [0:1100] f_stream(x) title "Stream fit" with lines ls 10 lw 2 dt ". ",\
	NaN title "Stream mean MAD" with points ls 40 pt 5 ps 0.7,\
	NaN notitle
# 	for [i=-k:k] f_CME(x)+i/k*sigma1 notitle with lines ls 11 lw 2 dt 1,\

print f_CME(2000.0)
print v_CME(9.0)
