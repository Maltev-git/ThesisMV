set terminal pdfcairo enhanced font "arial,12" fontscale 0.5 lw 1 size 18cm,8cm rounded

year = '2013'
month = '6'
day = '26'
durationDays = 6
date = year.'-'.month.'-'.day

start = `date --utc -d '2013-06-26' '+%s'`
end = start + 86400 * durationDays

filename = "example_sw_plot_v_b_".date."_".durationDays.".pdf"
set output filename

dataFile_ACE_64s = '/home/raid0/mvenzmer/Desktop/astro70/data/spacecraft/ACE/ACE_1h_and_64s_data/MAGSWE_64s_'.year.'.txt'
datafile_kp = "../Kp_3H_19630101--20161231_time_ap.txt"
datafile_sws_3hmax = "../../Data/OMNI_data2_processing/OMNI_HRO_1MIN_19810101--20161231_with_ap_VBzgsm_swsf_3hmax.txt"
datafile_sws_3hmin = "../../Data/OMNI_data2_processing/OMNI_HRO_1MIN_19810101--20161231_with_ap_VBzgsm_swsf_3hmin.txt"
datafile_OMNI_1min = "../../Data/OMNI_data2_processing/OMNI_HRO_1MIN_20130101--20131231_with_ap_VBzgsm_sws.txt"

####--  Style  --####
set style line 1 lw 1 lc rgb "white"
set style line 2 lw 1 lc rgb "light-red"
set style line 3 lw 1 lc rgb "web-green"
set style line 4 lw 1 lc rgb "web-blue"
set style line 5 lw 1 lc rgb "orange"
set style line 6 lw 1 lc rgb "dark-orange"
set style line 7 lw 1 lc rgb "skyblue"
set style line 8 lw 1 lc rgb "dark-blue"
set style line 9 lw 1 lc rgb "dark-magenta"
set style line 10 lw 1 lc rgb "black"
set style line 11 lw 1 lc rgb "gray"
set style line 12 lw 1 lc rgb "gray50"

set style line 50 lw 1 lc rgb "#7fffa500"
set style line 110 lw 1 lc rgb "#7fc0c0c0"

set style fill solid border -1


set multiplot\
	layout 3,1\
	margins 0.078,0.958,0.1,0.98\
	spacing 0,0

set border 3 front
set xtics nomirror
set ytics nomirror

####--  Key  --####
set key vert left top reverse Left box opaque
set key font ",11"
set key samplen 2
set key width -1
#unset key

set notitle

set angles degrees

### Axis options ###
set xdata time
set timefmt "%s"
set xrange [''.start:''.end]
set format x "%d"
set xtics 86400*1
set xtics add("" ''.start, "" ''.end)
set xtics offset 0,0.2
set mxtics 4


### Constants ###
ACEepoch = 820454400	#`date --utc -d 'Jan 1 00:00:00 UT 1996' '+%s'`
GnuplotEpoch = 0	#seconds since the Unix epoch `date --utc -d '1970-01-01 00:00 UTC' '+%s'`
#year: 5; doy: 6; ACEepoch: 7
timeColumn_ACE_64s = 7

#MAG and SWEPAM 64-second Averaged IMF and Solar Wind Ion Parameters
#1h & 64s invalid data value: -9.9999e+03
#	1-7	year hr min sec fp_year fp_doy ACEepoch
#	8-15	Np Tp Vp V_gse_x V_gse_y V_gse_z V_gsm_y V_gsm_z
#	16-21	B_gse_x B_gse_y B_gse_z B_gsm_y B_gsm_z Bmag
#	22-27	pos_gse_x pos_gse_y pos_gse_z pos_hs_x pos_hs_y pos_hs_z 

#1h: 117; 64s: 46
#fromLine_ACE_1h = 117
headerLines_ACE_64s = 46
fromLine_ACE_64s = ((month-1) * 30 + day-1) * 86400 / 64.0
toLine_ACE_64s = ((month-1) * 30 + day + durationDays+1) * 86400 / 64.0
fromLine = headerLines_ACE_64s + fromLine_ACE_64s
toLine = toLine_ACE_64s

ew = 1	#error width



set grid front #ls 12 dt 3
unset grid

unset key

set arrow front nohead from graph 0,graph 1 to graph 1,graph 1 ls 10 lw 1 dt 3
# every Xth data point
xth = 1

set timefmt "%Y-%m-%d %H"
set xtics add("" "2013-07-01")
set label "CME" at "2013-06-27 16",17.2 font ",11"
set timefmt "%s"

### Plots ###
set ylabel "Magnetic field\n[nT]" offset 0.1,0
set yrange [0:20]
set ytics 5
#set ytics add("" 0)
plot\
    datafile_OMNI_1min using (timecolumn(1,"%d-%m-%Y %H:%M:00.000")):(column(3)==9999.99?NaN:column(3)) notitle with impulses ls 7,\
	datafile_OMNI_1min using (timecolumn(1,"%d-%m-%Y %H:%M:00.000")):(column(3)==9999.99?NaN:column(3)) notitle with points ls 10 pt 7 ps 0,\
    datafile_sws_3hmax using (timecolumn(1,"%d-%m-%Y %H:%M:00.000")):(column(14)==1?16:NaN) notitle with lines ls 2 lw 2 dt 1,\
    datafile_sws_3hmax using (timecolumn(1,"%d-%m-%Y %H:%M:00.000") + 10800):(column(14)==1?16:NaN) notitle with lines ls 2 lw 2 dt 1,\
	NaN notitle
unset arrow
unset label


set key top right
set key width 0

set ylabel "Velocity\n[km{/: &{i}}s^{-1}]" offset 0.1,0
set yrange [300:700]
set ytics 100 offset 0,0
set format y "%g"
set ytics add("" 300)
set mytics 2
plot\
	datafile_OMNI_1min using (timecolumn(1,"%d-%m-%Y %H:%M:00.000")):(column(9)==99999.9?NaN:column(9)) notitle with impulses ls 7,\
	datafile_OMNI_1min using (timecolumn(1,"%d-%m-%Y %H:%M:00.000")):(column(9)==99999.9?NaN:column(9)) notitle with points ls 10 pt 7 ps 0,\
	datafile_sws_3hmax using (timecolumn(1,"%d-%m-%Y %H:%M:00.000") + 5400):(column(9)==999999.0?NaN:column(9)) notitle with histeps ls 6 lw 2,\
	NaN with histeps ls 6 lw 2 title "3-hour maximum",\
	NaN notitle
#####


Kp_neg(x) = 1.258 * log((x + 163.0)**2.0 + 1.1416e6) - 17.04
Kp_pos(x) = 0.467 * (Kp_neg(x) - Kp_neg(-163.0)) + Kp_neg(-163.0)

Kp_CME(x) = 10.6 * log(x + 810.0) - 73.0
Kp_stream(x) = 5.88 * log(x + 299.0) - 37.0

ap = "0 2 3 4 5 6 7 9 12 15 18 22 27 32 39 48 56 67 80 94 111 132 154 179 207 236 300 400"
#Kp = "0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26 27"
Kptoap(x) = word(ap,int(x + 1)) + 0
print Kptoap(27)
aptoKp(x) = int(x==0?0:x==2?1:x==3?2:x==4?3:x==5?4:x==6?5:x==7?6:x==9?7:x==12?8:x==15?9:x==18?10:x==22?11:x==27?12:x==32?13:x==39?14:x==48?15:x==56?16:x==67?17:x==80?18:x==94?19:x==111?20:x==132?21:x==154?22:x==179?23:x==207?24:x==236?25:x==300?26:x==400?27:NaN)
print aptoKp(400)


set arrow front nohead from graph 1,graph 0 to graph 1,graph 3 ls 10 lw 1 dt 3

set label at screen 0.03,screen 0.032 "2013"
set label at screen 0.81,screen 0.95 "OMNI minutely data"
set format x "%d %b"
unset xtics
set xtics 86400*1 nomirror
set xtics rotate by -30 out
#set xtics add("" ''.start, "" ''.end)


set key top right
set key width -1

set ylabel "{/:Italic Kp} index" offset -1,0
set yrange [0:9]
unset log
set ytics 3
set mytics 3
set format y "%g"
#set ytics add("" 1e6)
k = 1
plot\
	datafile_kp using (timecolumn(1,"%d-%m-%Y %H:%M:00.000") + 3600):($3/3) notitle with boxes ls 7,\
	datafile_sws_3hmax using (timecolumn(1,"%d-%m-%Y %H:%M:00.000")):(column(9)==999999.0?NaN:column(14)==1?Kp_CME(column(9))+1.1:Kp_stream(column(9))+0.7) notitle with fillsteps ls 50 lw 2,\
	datafile_sws_3hmax using (timecolumn(1,"%d-%m-%Y %H:%M:00.000")):(column(9)==999999.0?NaN:column(14)==1?Kp_CME(column(9))-1.1:Kp_stream(column(9))-0.7) notitle with fillsteps ls 1 lw 2,\
	datafile_sws_3hmax using (timecolumn(1,"%d-%m-%Y %H:%M:00.000") + 5400):((aptoKp(column(12))/3.0)>=(column(14)==1?Kp_CME(column(9))-1.1:Kp_stream(column(9))-0.7)?(column(14)==1?Kp_CME(column(9))-1.1:Kp_stream(column(9))-0.7):(aptoKp(column(12))/3.0)) notitle with boxes ls 7,\
	for [i=-k:k] datafile_sws_3hmax using (timecolumn(1,"%d-%m-%Y %H:%M:00.000") + 5400):(column(9)==999999.0?NaN:column(14)==1?Kp_CME(column(9))+1.1*i/k:Kp_stream(column(9))+0.7*i/k) notitle with histeps ls 5 lw 1,\
	datafile_sws_3hmax using (timecolumn(1,"%d-%m-%Y %H:%M:00.000") + 5400):(column(9)==999999.0?NaN:column(14)==1?Kp_CME(column(9)):Kp_stream(column(9))) notitle with histeps ls 6 lw 2,\
	NaN with boxes ls 7 title "Official {/:Italic Kp}",\
	NaN with histeps ls 6 lw 2 title "Estimated {/:Italic Kp}",\
	NaN with fillsteps ls 50 lw 0 title "Mean MAD",\
	NaN notitle
   # datafile_sws_3hmax using (timecolumn(1,"%d-%m-%Y %H:%M:00.000") + 5400):(aptoKp(column(12))/3.0) notitle with histeps ls 6 lw 2,\

