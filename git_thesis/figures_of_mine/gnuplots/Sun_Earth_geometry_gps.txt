set terminal pdfcairo enhanced font "arial,12" fontscale 0.5 lw 1 size 8.8cm,6.6cm rounded
set output "Sun_Earth_geometry_gps.pdf"

datafile_jpl = "/home/raid0/mvenzmer/Desktop/astro70/data/facilities/JPL/horizons_results_2000_without_header.txt"


#year = `date --utc -d 'today' '+%Y'`
#doy = `date --utc -d 'today' '+%-j'`
#today = year + doy/365.0 
#today = 2014.0 + 11.0/12.0 + 7.0/365.0

vernal_equinox = 2016.0 + 2.0/12.0 + 20.0/366.0
#omega = 73.67 + 0.013958 * (today - 1850.0)
#solarTilt = 7.25	#solar tilt
#beta = -omega + (-vernal_equinox + today) * 360.0
#tilt = solarTilt * sin(beta)	#projected tilt from earth
tilt(x) = 7.25 * sin(-(73.67 + 0.013958 * (x - 1850.0)) + (-vernal_equinox + x) * 360.0)

#Earth tilt
tilt_E(x) = 23.44 * sin((-vernal_equinox + x) * 360.0)

#Earth distance to Sun
perihelion = 2016.0 + 2.0/366.0
distance(x) = 1.0 - 0.0167 * cos((-perihelion + x) * 360.0)


####--  Styles  --####
set style line 1 lt 1 lw 1 lc rgb "white"
set style line 2 lt 1 lw 1 lc rgb "light-red"
set style line 3 lt 1 lw 1 lc rgb "web-green"
set style line 4 lt 1 lw 1 lc rgb "web-blue"
set style line 5 lt 1 lw 1 lc rgb "yellow"
set style line 6 lt 1 lw 1 lc rgb "brown"
set style line 7 lt 1 lw 1 lc rgb "light-blue"
set style line 8 lt 1 lw 1 lc rgb "dark-blue"
set style line 9 lt 1 lw 1 lc rgb "dark-violet"
set style line 10 lt 1 lw 1 lc rgb "black"
set style line 11 lt 1 lw 1 lc rgb "grey"
set style line 12 lt 1 lw 1 lc rgb "grey50"

set multiplot\
	layout 1,1\
	margins 0.12,0.855,0.095,0.98\
	spacing 0,0

set border 3
set xtics nomirror
set ytics nomirror
set tics scale 0.75 front

set key right top reverse Left box opaque
set key font ",11"
set key samplen 2
set key width -4.5
set key height +0.1
set key bottom center

set angles degrees

set border 11

set grid front

set xrange [1:13]
set xtics 1 offset 0.5,-1.2 rotate by 60
set xtics add("Jan" 1,"Feb" 2,"Mar" 3,"Apr" 4,"May" 5,"Jun" 6,"Jul" 7,"Aug" 8,"Sep" 9,"Oct" 10,"Nov" 11,"Dec" 12,"" 13) font ",11"

set ylabel "Tilt angle" offset 2.3,0
set yrange [-30:30]
set ytics 10
set mytics 2
set format y "%gÂ°"

set y2label "Solar distance [au]" tc ls 4 offset -1,0
set y2range [0.97:1.03]
set y2tics 0.01 tc ls 4
set my2tics 2

plot\
	0 with lines notitle ls 10 dt 2,\
	tilt(2016.0+(x-1.0)/12.0) with lines title "Solar tilt to ecliptic" ls 10 lw 2,\
	tilt_E(2016.0+(x-1.0)/12.0) with lines title "Earth tilt to ecliptic" ls 10 lw 2 dt 2,\
	distance((x-1.0)/12.0) axis x1y2 with lines title "Earth distance to Sun" ls 4 lw 2 dt 1,\
	NaN notitle



