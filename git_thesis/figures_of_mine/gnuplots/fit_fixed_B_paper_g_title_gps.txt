set terminal pdfcairo enhanced font "arial,12" fontscale 0.5 lw 1 size 18cm,9cm rounded
set output "fit_fixed_B_paper_g_title.pdf"

datafile_H12_1h = "../data/HELIOS12_COHO1HR_MERGED_MAG_PLASMA_combined_noheader.txt"
#14 B
datafile_H12_1h_statisticsB = "../data/HELIOS12_COHO1HR_2Dhistogram_combined_r_vs_B_05nT_dependenceFunctionsYofX.txt"
datafile_H12_1h_2DhistogramB = "../data/HELIOS12_COHO1HR_2Dhistogram_combined_r_vs_B_05nT.txt"
binSizeB = 0.5

####--  Style  --####
set style line 1 lw 1 lc rgb "white"
set style line 2 lw 1 lc rgb "light-red"
set style line 3 lw 1 lc rgb "web-green"
set style line 4 lw 1 lc rgb "web-blue"
set style line 5 lw 1 lc rgb "orange"
set style line 6 lw 1 lc rgb "dark-orange"
set style line 7 lw 1 lc rgb "skyblue"
set style line 8 lw 1 lc rgb "dark-blue"
set style line 9 lw 1 lc rgb "dark-magenta"
set style line 10 lw 1 lc rgb "black"
set style line 11 lw 1 lc rgb "gray"
set style line 12 lw 1 lc rgb "gray50"

#median line style
set style line 100 lt 1 lw 2 dt 1 lc rgb "white"

set style textbox opaque noborder


set multiplot\
	layout 1,1\
	margins 0.07,0.92,0.1,0.995\
	spacing 0,0

####--  Key  --####
set key reverse Left box opaque
set key width -3
set key font ",11"
#set key samplen 1
set key at 1.02,200
unset key

set tics scale 0.5

set border 10 front
set xtics nomirror
set ytics nomirror


PSPperihel=0.0459

#boundaries
perihel=0.285
aphel=0.98


a = 4.08078
b = -1.85771
b2 = -1.32237
avg(x) = a * ((x**b)**2.0 + (x**b2)**2.0)**0.5
a_err = 0.0291872
b_err = 0.042217
b2_err = 0.115887

c = 3.83306
d = b
d2 = b2
med(x) = c * ((x**d)**2.0 + (x**d2)**2.0)**0.5
c_err = 0.0265236
d_err = b_err
d2_err = b2_err

mean(x) = avg(x)
median(x) = med(x)

### !! old error calculation !!
mean_error(r) = sqrt((r**b*a_err)**2.0 + (a*r**b*log(r)*b_err)**2.0)
median_error(r) = sqrt((r**d*c_err)**2.0 + (c*r**d*log(r)*d_err)**2.0)
sigma(r) = sqrt(2.0 * log(mean(r) / median(r)))
W(r,n) = 1 / (sqrt(2.0*pi) * sigma(r) * n) * exp(-log(n / median(r))**2.0 / (2.0 * sigma(r)**2.0))
#partial derivatives of 1/(sqrt(2 pi)*sqrt(2*ln((a*r^b)/(c*r^d)))*n) * exp(-(ln(n/(c*r^d)))^2/(2*2*ln((a*r^b)/(c*r^d)))) for d
dWda(r,n) = -(exp(-(log((n*r**(-d))/c)**2.0)/(4.0*log((a*r**(b-d))/c)))*(2.0*log((a*r**(b-d))/c)-log((n*r**(-d))/c)**2.0))/(8.0*sqrt(pi)*a*n*log((a*r**(b-d))/c)**(5.0/2.0))
dWdb(r,n) = -(log(r)*exp(-(log((n*r**(-d))/c)**2.0)/(4.0*log((a*r**(b-d))/c)))*(2.0*log((a*r**(b-d))/c)-log((n*r**(-d))/c)**2.0))/(8.0*sqrt(pi)*n*log((a*r**(b-d))/c)**(5.0/2.0))
dWdc(r,n) = (exp(-(log((n*r**(-d))/c)**2.0)/(4.0*log((a*r**(b-d))/c)))*(2.0*log((n*r**(-d))/c)*log((a*r**(b-d))/c)+2.0*log((a*r**(b-d))/c)-log((n*r**(-d))/c)**2.0))/(8.0*sqrt(pi)*c*n*log((a*r**(b-d))/c)**(5.0/2.0))
dWdd(r,n) = (log(r)*exp(-(log((n*r**(-d))/c)*2.0)/(4.0*log((a*r**(b-d))/c)))*(2.0*log((n*r**(-d))/c)*log((a*r**(b-d))/c)+2.0*log((a*r**(b-d))/c)-log((n*r**(-d))/c)**2.0))/(8.0*sqrt(pi)*n*log((a*r**(b-d))/c)**(5.0/2.0))
W_error(r,n) = sqrt((dWda(r,n)*a_err)**2.0 + (dWdb(r,n)*b_err)**2.0 + (dWdc(r,n)*c_err)**2.0 + (dWdd(r,n)*d_err)**2.0)

a_err = 0.0291872
b_err = 0.042217
b2_err = 0.115887
c_err = 0.0265236
d_err = b_err
d2_err = b2_err

#### axis settings
set pm3d corners2color c3
set view map
set isosamples 200
#set samples 30
#set grid front
set tics scale 0.5

set xrange [0.0:1.0]

####--  Plots  --####
set log y
set ylabel "Magnetic field strength [nT]" offset 0.1,0
lowlim = 1.0
uplim = 5000
set yrange [lowlim:uplim]
set ytics add("" uplim)
set ytics offset 0.5,0
set xtics 0.2 offset 0,0.5
set mxtics 2
#set xtics add("" 1.02)

set log cb
set format cb "10^{%T}"
set format cb ""
set cbrange [1e-4:1]
set cbtics #offset -0.5,0
unset colorbox
#set palette defined (0 "white", 0.001 "blue", 0.05 "cyan", 0.09 "green", 0.15 "yellow", 0.22 "orange", 0.37 "red", 0.5 "violet", 1 "white")
#cubehelix palette
#https://www.mrao.cam.ac.uk/~dag/CUBEHELIX/
#https://www.mrao.cam.ac.uk/~dag/CUBEHELIX/cubetry.html
set palette cubehelix start 2.0 cycles 1.4 saturation 1.4 negative
set palette gamma 1.0
##############################

set label "Helios data" at graph 0.6,graph 0.9 front boxed
set label "Extrapolation" at graph 0.09,graph 0.9 front boxed
set arrow from 0.285,lowlim to 0.285,uplim nohead ls 10 lw 1 dt "_ " front
set arrow from PSPperihel,lowlim to PSPperihel,uplim nohead ls 10 lw 1 dt "_ " front


set colorbox user origin graph 1.03,0 size graph 0.03,1
set cblabel "Relative frequency" offset 0.7,0
set xlabel "Solar distance [au]" offset 0,0.7
splot\
	datafile_H12_1h_2DhistogramB index 1 matrix using ($1*0.01+0.285):($2*binSizeB+lowlim):(column(3)):(column(3)) with pm3d lc palette notitle,\
	datafile_H12_1h_statisticsB using ($1+0.005):($5==0?NaN:$5):0 with lines ls 100,\
	W(x<PSPperihel?NaN:x>0.285?NaN:x,y>uplim-1?NaN:y)*binSizeB with pm3d notitle lc palette,\
	'+' using ($1<PSPperihel||$1>0.285?NaN:$1):(median($1)):0 with lines ls 100,\
	NaN notitle

