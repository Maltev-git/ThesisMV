set terminal pdfcairo enhanced font "arial,12" fontscale 0.5 lw 1 size 8.8cm,8.8cm rounded
set output "lognormal_ms_b.pdf"

##### define function
sigma = 1
mu = 0
f(x,mu,sigma) = 1 / (x * sigma * sqrt(2 * pi)) * exp(-(log(x) - mu)**2 / (2 * sigma**2))

gaussian(x,mu,sigma) = 1 / (sigma * sqrt(2 * pi)) * exp(-(x - mu)**2 / (2 * sigma**2))


####--  Style  --####
set style line 1 lw 1 lc rgb "white"
set style line 2 lw 1 lc rgb "light-red"
set style line 3 lw 1 lc rgb "web-green"
set style line 4 lw 1 lc rgb "web-blue"
set style line 5 lw 1 lc rgb "orange"
set style line 6 lw 1 lc rgb "dark-orange"
set style line 7 lw 1 lc rgb "skyblue"
set style line 8 lw 1 lc rgb "dark-blue"
set style line 9 lw 1 lc rgb "dark-magenta"
set style line 10 lw 1 lc rgb "black"
set style line 11 lw 1 lc rgb "gray"
set style line 12 lw 1 lc rgb "gray50"

set style line 18 lw 1 lc rgb "slateblue1"
set style line 49 lw 1 lc rgb "purple"
set style line 43 lw 1 lc rgb "sea-green"
set style line 44 lw 1 lc rgb "blue"


set multiplot\
	layout 2,1\
	margins 0.135,0.98,0.095,0.985\
	spacing 0,0.1

set border 3 front
set xtics nomirror
set ytics nomirror

set key vert right top reverse Left box opaque
set key font ",11"
set key samplen 2
set key width -1

set grid

##### axes
set xrange [0:5]
set yrange [0:1.5]
set xtics 1
set ytics 0.5


set label "{/Symbol s} = 1" at 2.1,1.1
set ylabel "f(x)" offset 0.3,0

plot\
	f(x,1,1) with lines ls 7 lw 2 title "{/Symbol m} = 1",\
	f(x,0.5,1) with lines ls 4 lw 2 title "{/Symbol m} = 0.5",\
	f(x,0,1) with lines ls 44 lw 2 title "{/Symbol m} = 0",\
	f(x,-0.5,1) with lines ls 8 lw 2 title "{/Symbol m} = -0.5",\
	f(x,-1,1) with lines ls 10 lw 2 title "{/Symbol m} = -1",\
	NaN notitle

set key width -0
set label 1 "{/Symbol m} = 0" at 2.1,1.1
set xlabel "x" offset 0,0.3
plot\
	gaussian(x,1,0.3) with lines ls 10 lw 2 dt 2 title "Gaussian",\
	f(x,0,0.3) with lines ls 3 lw 2 title "{/Symbol s} = 0.3",\
	f(x,0,0.5) with lines ls 43 lw 2 title "{/Symbol s} = 0.5",\
	f(x,0,1) with lines ls 44 lw 2 title "{/Symbol s} = 1",\
	f(x,0,1.5) with lines ls 18 lw 2 title "{/Symbol s} = 1.5",\
	f(x,0,2.0) with lines ls 49 lw 2 title "{/Symbol s} = 2",\
	NaN notitle

