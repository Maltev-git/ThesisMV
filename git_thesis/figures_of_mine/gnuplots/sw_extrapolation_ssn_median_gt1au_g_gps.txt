set terminal pdfcairo enhanced font "arial,12" fontscale 0.5 lw 1 size 8.8cm,6.6cm rounded
set output "sw_extrapolation_ssn_median_gt1au_g.pdf"

datafile_model_s = "model_s.txt"
datafile_Parker_wind_Wang = "Parker_sw_model/Parker_wind_T1e6_R5.8.txt"
datafile_Parker_wind_Sheeley = "Parker_sw_model/Parker_wind_T1.1e6_R5.txt"
datafile_Vmedian = "Python_calculate_Vmedian/Vmedian_vs_r_ssn_b.txt"

####--  Style  --####
set style line 1 lw 1 lc rgb "white"
set style line 2 lw 1 lc rgb "light-red"
set style line 3 lw 1 lc rgb "web-green"
set style line 4 lw 1 lc rgb "web-blue"
set style line 5 lw 1 lc rgb "orange"
set style line 6 lw 1 lc rgb "dark-orange"
set style line 7 lw 1 lc rgb "skyblue"
set style line 8 lw 1 lc rgb "dark-blue"
set style line 9 lw 1 lc rgb "dark-magenta"
set style line 10 lw 1 lc rgb "black"
set style line 11 lw 1 lc rgb "gray"
set style line 12 lw 1 lc rgb "gray50"

set style line 28 lw 1 lc rgb "dark-red"
set style fill transparent solid 1 noborder


set multiplot\
	layout 1,1\
	margins 0.125,0.995,0.12,0.998\
	spacing 0,0

####--  Key  --####
set key reverse Left box opaque
set key font ",11"
set key samplen 2
#unset key

set border 7 front
set xtics nomirror
set ytics nomirror

# square-root power-law function
aa_med = 3.833
e_b = -1.858
e_b2 = -1.32
med(r) = aa_med * ((r**e_b)**2.0 + (r**e_b2)**2.0)**0.5
median(r,ssn) = (a_ssn * ssn + b_ssn) / sqrt(2.0) * ((r**e_b)**2.0 + (r**e_b2)**2.0)**0.5


#median(r,ssn) = (a_ssn * ssn + b_ssn) * r**e
mean(r,ssn) = median(r,ssn) * c_ssn

sigma(r,ssn) = sqrt(2.0 * log(mean(r,ssn) / median(r,ssn)))

W(r,n) = 1 / (sqrt(2.0*pi) * sigma(r,ssn) * n) * exp(-log(n / median(r,ssn))**2.0 / (2.0 * sigma(r,ssn)**2.0))

Rs = 695700.0
au = 149597870.7

# Banaszkiewicz1998 [0:1]
Q=1.5
K=1
a1=1.538
M=1.789
Banaszkiewicz1998_rho(r) = M * (K/a1 * r/(a1**2.0 + r**2.0)**(3.0/2.0))
Banaszkiewicz1998_z(r) = M * (-r**(-3.0) + 3.0*Q/8.0 * 3.0/r**5.0 + (K/(a1**2.0 + r**2.0)**(3.0/2.0)))
Banaszkiewicz1998(x) = (Banaszkiewicz1998_rho(x)**2.0 + Banaszkiewicz1998_z(x)**2.0)**(1.0/2.0)
print Banaszkiewicz1998(1)

# Parker B-field
B_Parker_r(r) = 4.04 * r**-1.852
B_Parker_phi(r) = 4.04 * r**-0.97
B_Parker(r) = sqrt(B_Parker_r(r)**2.0 + B_Parker_phi(r)**2.0)
med_Parker(r) = 5.661 / sqrt(2.0) * ((r**-2.0)**2.0 + (r**-1.0)**2.0)**0.5

# # Parker1958 isothermal solution
R_c = 5.8*Rs/au	# critical radius [au]
# T_corona = 1e6	# isothermal coronal temperature [K]
# Mp = 1.672621898e-27 # proton mass [kg]
# k_B = 1.38064852e-23 # Boltzmann constant [J/K]
# U(T_corona) = sqrt(2.0*k_B*T_corona/Mp)/1000 # critical speed [km/s]
# V_Parker1958_lt1(r) = U(T_corona) * R_c**2.0/r**2.0 * exp(3.0/2.0 - 2.0*R_c/r)
# V_Parker1958_gt1(r) = U(T_corona) * sqrt(4.0 * log(r/R_c) - 0.0)
# print U(T_corona)
# print V_Parker1958_lt1(0.04)
# print V_Parker1958_gt1(0.3)
# W_0(x) = -1.0 + sqrt(2.0 + 2.0*exp(1.0)*x)	#approx for r = R_c
# W_1(x) = -1.0 - sqrt(2.0 + 2.0*exp(1.0)*x)	#approx for r = R_c
# V_Parker1958_neg(r) = U(T_corona) * -{0.0,1.0} * sqrt(lambertw(-(exp(3.0 - 4.0*R_c/r) * R_c**4.0)/r**4.0))
# V_Parker1958_pos(r) = U(T_corona) * -{0.0,1.0} * sqrt(W_1(-(exp(3.0 - 4.0*R_c/r) * R_c**4.0)/r**4.0))
# print V_Parker1958_neg(1)
# print V_Parker1958_pos(1)
# D(r) = R_c/r * exp(4.0 * (1.0 - R_c/r) - 1.0)
# V_Cranmer2004_lt1(r) = U(T_corona) * sqrt(-lambertw(-D(r)))
# V_Cranmer2004_gt1(r) = U(T_corona) * sqrt(-W_1(-D(r)))

# MVVB2018 field
MVVB2018(r) = 5.377 * r**-1.655

#### axis settings
xPSPperihel = 0.0459
xPSPdata = 0.25
xssurface = Rs/au	#0.00465046726096216
xh2perihel = 0.29
xhaphel = 0.98
xrlow = 0.03
xrup = 20
set xrange [xrlow:xrup]
#set xtics 0.05
set mxtics 10
#set xtics add(sprintf("%g",xPSPperihel) xPSPperihel, "0.2" 0.2, sprintf("%g",xPSPdata) xPSPdata, "0.3" 0.3)
set xtics add("" xrup)
set xtics offset 0,0

#set link x2 via x*au/Rs inverse x/au*Rs
set x2tics offset 0,-2.2
set x2range [xrlow*au/Rs:xrup*au/Rs]
#set format x2 "%g"
set x2label "[{/:Italic=R}_â˜‰]" offset -2,-4.5
set log x2

set grid back

######## B #########
a = 5.705
b = -1.662
c = 5.358
d = b
a_err = 0.028
b_err = 0.011
c_err = 0.025
d_err = b_err

s2low = 2.4810850
s2up = 12.9436
s2low(r) = s2low * r**b
s2up(r) = s2up * r**b

a_ssn = 1.309e-2
b_ssn = 4.285
c_ssn = 1.08786
e = -1.662

set log xy
set mytics 10
set ylabel "Magnetic field strength [nT]" offset 0,0
set format y "10^{%T}"
ylow = 1e-1
yup = 5e3
set yrange [ylow:yup]
#set key at xrup,graph 0.85
set key height +0.2
set key width -2
set ytics add("10 " 10, "1 " 1)
# Helios range
yH2 = 0.05
set arrow from xh2perihel,graph yH2 to xhaphel,graph yH2 nohead ls 3 dt 1 lw 2 front
set label "Helios" font ",11" at xh2perihel,graph yH2*1.5 front
# PSP range
yPSP = yH2
set arrow from xPSPperihel,graph yPSP to xPSPdata,graph yPSP nohead ls 4 dt 1 lw 2 front
set label "PSP" font ",11" at xPSPperihel,graph yPSP*1.5 front
# Sun
#set arrow from xssurface,ylow to xssurface,yup nohead ls 12 dt 2 lw 1 back
set xlabel "Solar distance [au]" offset 0,0.5
plot\
	MVVB2018(x) notitle with lines ls 5 lw 2 dt 1,\
	NaN title "|{/:Italic= B}_{med}| (Parker)" with lines ls 10 lw 2,\
	B_Parker_r(x) title "{/:Italic= B_r}" with lines ls 12 lw 2 dt 2,\
	B_Parker_phi(x) title "{/:Italic= B_{/Symbol f}}" with lines ls 12 lw 2 dt 3,\
	B_Parker(x) notitle with lines ls 10 lw 2,\
	NaN title "|{/:Italic= B}_{med}| (paper)" with lines ls 5 lw 2 dt 1,\
	NaN notitle

